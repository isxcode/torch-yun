plugins {
    id 'java'
    id 'checkstyle'
    alias(libs.plugins.springboot)
    alias(libs.plugins.springboot.dependency)
    alias(libs.plugins.spotless)
}

def version_number = new File(rootDir.getAbsolutePath(), 'VERSION').readLines()[0].trim()

allprojects {

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    group = 'com.isxcode.torch'
    version = version_number

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    configurations.configureEach {
        exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
        exclude group: "org.slf4j", module: "slf4j-reload4j"
        exclude group: "org.slf4j", module: "slf4j-simple"
    }

    dependencies {

        implementation libs.spring.web
        implementation libs.spring.aop
        implementation libs.spring.validation
        annotationProcessor libs.spring.configuration.processor
        testImplementation libs.spring.test
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        implementation libs.fastjson
        implementation libs.swagger
        implementation libs.junit.jupiter
        testImplementation libs.junit.jupiter.api
        implementation libs.mapstruct.main
        implementation libs.mapstruct.processor
        annotationProcessor libs.mapstruct.processor
        testAnnotationProcessor libs.mapstruct.processor
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.withType(Checkstyle).configureEach {
        maxWarnings = 100
        maxErrors = 0
        ignoreFailures = false
        config resources.text.fromFile(rootDir.getAbsolutePath() + '/.checkstyle/checkstyle.xml')
        reports {
            xml.required = false
            html.required = true
            html.stylesheet resources.text.fromFile(rootDir.getAbsolutePath() + '/.checkstyle/checkstyle-simple.xsl')
        }
    }
}

spotless {
    java {
        target '*/**/*.java'
        targetExclude('*/build/**/*.java', 'torch-yun-dist/**/*.java')
        eclipse().configFile(rootDir.getAbsolutePath() + '/.spotless/eclipse-java-google-style.xml')
        removeUnusedImports()
    }
}

tasks.register("format") {

    dependsOn(":torch-yun-dist:copy_version", ":spotlessApply", ":checkstyleMain")
}

tasks.register("package") {
    dependsOn(":torch-yun-dist:make")
}

tasks.register('start', Exec) {
    commandLine 'sh', '-c', '''
      rm -rf torch-yun-dist/build/distributions/zhishuyun
      tar -zxf torch-yun-dist/build/distributions/zhishuyun.tar.gz -C torch-yun-dist/build/distributions/
      cd torch-yun-dist/build/distributions/zhishuyun/bin
      bash start.sh
    '''
}

tasks.register('website', Exec) {
    commandLine 'sh', '-c', '''
      cd docs
      pnpm install --force
      pnpm run dev
     '''
}

tasks.register('frontend', Exec) {
    commandLine 'sh', '-c', '''
      cd torch-yun-frontend
      pnpm install --force
      pnpm run dev --host
    '''
}

tasks.register('backend') {
    dependsOn(":torch-yun-backend:torch-yun-main:bootRun")
}

tasks.register('install', Exec) {
    commandLine 'sh', '-c', 'bash download.sh && bash install.sh'
}

tasks.register('docker', Exec) {
    commandLine 'sh', '-c', '''
      docker buildx uninstall
      docker build -t isxcode/zhishuyun:''' + version_number + ''' -f ./Dockerfile .
    '''
}

tasks.register('upload-ali-oss', Exec) {

    commandLine 'sh', '-c', '''
      ssh-copy-id root@zhishuyun.isxcode.com
      scp torch-yun-dist/build/distributions/zhishuyun.tar.gz root@zhishuyun.isxcode.com:/data/file/
    '''
}

tasks.register('upload-docker-hub', Exec) {

    commandLine 'sh', '-c', '''
      docker buildx install
      docker buildx build --platform linux/amd64,linux/arm64/v8 -t isxcode/zhishuyun:''' + version_number + ''' -f ./Dockerfile . --push
    '''
}

tasks.register('upload-ali-hub', Exec) {

    commandLine 'sh', '-c', '''
      docker buildx install
      docker buildx build --platform linux/amd64 -t registry.cn-shanghai.aliyuncs.com/isxcode/zhishuyun:''' + version_number + '''-amd64 -f ./Dockerfile . --push
      docker buildx build --platform linux/arm64/v8 -t registry.cn-shanghai.aliyuncs.com/isxcode/zhishuyun:''' + version_number + '''-arm64 -f ./Dockerfile . --push
    '''
}